// INSPECTOR TEQUILA! - from the movie Hard Boiled.
// Replacement for punisher so clients must still reload to avoid constant weapon spray.

/* CVARS - copy and paste to shconfig.cfg

//Inspector Tequila
insptequila_level 0

*/

#include <amxmodx>
#include <superheromod>

// GLOBAL VARIABLES
new HeroName[] = "Inspector Tequila"
new bool:HasInspectorTequila[SH_MAXSLOTS+1]

// Temp lookup table (from csdm), will be in next sh version
new weapon_slot[] = {
	0,
	2,	//CSW_P228
	0,
	1,	//CSW_SCOUT
	4,	//CSW_HEGRENADE
	1,	//CSW_XM1014
	5,	//CSW_C4
	1,	//CSW_MAC10
	1,	//CSW_AUG
	4,	//CSW_SMOKEGRENADE
	2,	//CSW_ELITE
	2,	//CSW_FIVESEVEN
	1,	//CSW_UMP45
	1,	//CSW_SG550
	1,	//CSW_GALIL
	1,	//CSW_FAMAS
	2,	//CSW_USP
	2,	//CSW_GLOCK18
	1,	//CSW_AWP
	1,	//CSW_MP5NAVY
	1,	//CSW_M249
	1,	//CSW_M3
	1,	//CSW_M4A1
	1,	//CSW_TMP
	1,	//CSW_G3SG1
	4,	//CSW_FLASHBANG
	2,	//CSW_DEAGLE
	1,	//CSW_SG552
	1,	//CSW_AK47
	3,	//CSW_KNIFE
	1	//CSW_P90
}
// Temp Lookup table, will be in next sh version
new max_bp_ammo[] = {
        0,
        52,     //CSW_P228
        0,
        90,     //CSW_SCOUT
        1,      //CSW_HEGRENADE
        32,     //CSW_XM1014
        1,      //CSW_C4
        100,	//CSW_MAC10
        90,     //CSW_AUG
        1,      //CSW_SMOKEGRENADE
        120,	//CSW_ELITE
        100,	//CSW_FIVESEVEN
        100,	//CSW_UMP45
        90,     //CSW_SG550
        90,     //CSW_GALIL
        90,     //CSW_FAMAS
        100,	//CSW_USP
        120,	//CSW_GLOCK18
        30,     //CSW_AWP
        120,	//CSW_MP5NAVY
        200,	//CSW_M249
        32,     //CSW_M3
        90,     //CSW_M4A1
        120,	//CSW_TMP
        90,     //CSW_G3SG1
        2,      //CSW_FLASHBANG
        35,     //CSW_DEAGLE
        90,     //CSW_SG552
        90,     //CSW_AK47
        0,      //CSW_KNIFE
        100	//CSW_P90
}
//----------------------------------------------------------------------------------------------
public plugin_init()
{
	// Plugin Info
	register_plugin("SUPERHERO Inspector Tequila", "1.0", "vittu")

	// DO NOT EDIT THIS FILE TO CHANGE CVARS, USE THE SHCONFIG.CFG
	register_cvar("insptequila_level", "0")

	// FIRE THE EVENT TO CREATE THIS SUPERHERO!
	shCreateHero(HeroName, "Unlimited Ammo Clips", "Endless ammo clips, reload required", false, "insptequila_level")

	// REGISTER EVENTS THIS HERO WILL RESPOND TO! (AND SERVER COMMANDS)
	// INIT
	register_srvcmd("insptequila_init", "insptequila_init")
	shRegHeroInit(HeroName, "insptequila_init")

	// EVENTS
	register_event("CurWeapon", "change_weapon", "be", "1=1")
}
//----------------------------------------------------------------------------------------------
public insptequila_init()
{
	// First Argument is an id
	new temp[6]
	read_argv(1, temp, 5)
	new id = str_to_num(temp)

	// 2nd Argument is 0 or 1 depending on whether the id has the hero
	read_argv(2, temp, 5)
	new hasPowers = str_to_num(temp)

	switch(hasPowers)
	{
		case true:
		{
			HasInspectorTequila[id] = true

			if ( is_user_alive(id) )
				fill_ammo(id)
		}

		case false:
			HasInspectorTequila[id] = false
	}
}
//----------------------------------------------------------------------------------------------
fill_ammo(id)
{
	if ( !shModActive() || !is_user_alive(id) || !HasInspectorTequila[id] )
		return

	new clip, ammo, wpnid = get_user_weapon(id, clip, ammo)

	new slot = weapon_slot[wpnid]

	// Make sure its a gun
	if ( slot == 1 || slot == 2 )
	{
		new maxBpAmmo = max_bp_ammo[wpnid]

		if ( cs_get_user_bpammo(id, wpnid) < maxBpAmmo )
			cs_set_user_bpammo(id, wpnid, maxBpAmmo)
	}
}
//----------------------------------------------------------------------------------------------
public change_weapon(id)
{
	// Should prob avoid calling this during spawn
	if ( !shModActive() || !HasInspectorTequila[id] )
		return

	new wpnid = read_data(2)

	new slot = weapon_slot[wpnid]

	// Make sure its a gun
	if ( slot == 1 || slot == 2 )
	{
		new maxBpAmmo = max_bp_ammo[wpnid]

		if ( cs_get_user_bpammo(id, wpnid) < maxBpAmmo )
			cs_set_user_bpammo(id, wpnid, maxBpAmmo)
	}
}
//----------------------------------------------------------------------------------------------