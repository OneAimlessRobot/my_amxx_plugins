#include <amxmodx> 
#include "../my_include/superheromod.inc"
#include <fun>
#include <cstrike>

/* CVARS - copy and paste to shconfig.cfg

// Achilles
achilles_level 8     - level at which he becomes available
achilles_dmgmult 3.0 - multiplier of the damage he takes in the left leg; set to 100 for instant death

//All credit must go to Mydas for dreaming him up and creating the hero
*
* V2.1 - Mydas 4/29/2008, revisiting old heroes
*   Changed the way users are informed of certain things ** most important - Achilles' death (from "Achilles' Heel" to the name of attacker's weapon)
*	The users are no longer informed each round but each time a player gets or drops Achilles
*   Removed CVAR achilles_inform, it's useless

* V2.0 - Burcyril10 12/8/2005
*	Fixed a logic bug: the hitzones MUST be set AFTER the round has started (hence the timer) - don't ask why, no idea pure fluke i caught the bug
*	Cleaned up code, commented it
*	Inserted the 'inform' cvar - not being told your opponent was very unfair especialy if you don't have anubis.
*		- Note: It only tells you at the start of the round their name it doesn't say it over their head or anything overly lame
*	Tested for about 3 weeks, found optimal cvar settings.
*	Note: Achilles's body is only bullet proof - nades, knives and powers work exactly the same (I left it that way because Achilles is balanced enough)

* V1.0 - Mydas 2005
*	THE HERO IS BORN...
*/

// VARIABLES 
new gHeroName[] = "Achilles" 
new bool:gHasAchillesPower[SH_MAXSLOTS + 1] 
//----------------------------------------------------------------------------------------------
public plugin_init() 
{
   // Plugin Info
   register_plugin("SUPERHERO Achilles", "2.1", "Mydas/Burcyril10") 

   // DO NOT EDIT THIS FILE TO CHANGE CVARS, USE THE SHCONFIG.CFG
   register_cvar("achilles_level", "8" )
   register_cvar("achilles_dmgmult", "3.0") 
  
   // FIRE THE EVENT TO CREATE THIS SUPERHERO!
   debugMessage("Attempting to create Achilles Hero")
   shCreateHero(gHeroName, "Partial Immortality", "Bullets can only hit you in the left leg, but they hit you for more damage - don't fall !", false, "achilles_level") 
  
   // REGISTER EVENTS THIS HERO WILL RESPOND TO! (AND SERVER COMMANDS) INIT 
   register_event("Damage", "achilles_damage", "b","2!0")
   register_event("ResetHUD","newRound","b")

   // INIT
   register_srvcmd("achilles_init", "achilles_init") 
   shRegHeroInit(gHeroName, "achilles_init") 
} 
//----------------------------------------------------------------------------------------------
public achilles_init() 
{ 
	new temp[6] 
	// First Argument is an id 
	read_argv(1, temp, 5) 
	new id=str_to_num(temp) 

	// 2nd Argument is 0 or 1 depending on whether the id has Achilles powers 
	read_argv(2, temp, 5) 
	new hasPowers=str_to_num(temp) 

	gHasAchillesPower[id]=(hasPowers!=0)

	if (is_user_connected(id))
		if (gHasAchillesPower[id]) {
			//Since they have the power make it so that they only have left leg hitzone active (THIS DOES NOT INCLUDE KNIVES OR NADES)
			set_user_hitzones(0, id, 64)
			set_user_info(id,"ACHI","1")
		
			//Inform the players somebody has picked Achilles
			new name[18] 
			get_user_name(id, name, 17)
			client_print(0,print_chat,"[SH](Achilles) %s has the power of Achilles. Bullets can only hit their left leg.",name)
		}
		else {
			//they don't have achilles, let them be hit everywhere
			set_user_hitzones(0, id, 255)
			set_user_info(id,"ACHI","0")
	
			//Inform the players somebody has dropped Achilles
			new name[18] 
			get_user_name(id, name, 17)
			client_print(0,print_chat,"[SH](Achilles) %s no longer has the power of Achilles. Bullets can hit them as normal.",name)
		}
} 
//----------------------------------------------------------------------------------------------
//Fire this when he is hurt
public achilles_damage(id) 
{
	if (!shModActive() || !is_user_alive(id)) return PLUGIN_CONTINUE
	
	new damage = read_data(2)
  	new weapon, bodypart, attacker = get_user_attacker(id, weapon, bodypart)

	//Calcuate extra damage - inflict it
	new extradamage = floatround(damage*get_cvar_float("achilles_dmgmult") - damage)
	new wpnid, clip, ammo, wpn[32] = "fall/explosion"	// default the weapon name to an neutral source of damage
	
	wpnid = get_user_weapon(attacker, clip, ammo)
	if (wpnid > 0) 
		get_weaponname(wpnid,wpn,31) // if a weapon was used, the weapon name is used
	
	replace(wpn, 31, "weapon_", "" )
	shExtraDamage(id, attacker, extradamage, wpn)
	return PLUGIN_HANDLED
}
//----------------------------------------------------------------------------------------------
//Inform the client that whoever has achilles
public newRound(id)
{
	if ( gHasAchillesPower[id] ) 
	   	set_user_hitzones(0, id, 64)
	else
	    set_user_hitzones(0, id, 255)

	return PLUGIN_HANDLED
}
//----------------------------------------------------------------------------------------------
/* AMXX-Studio Notes - DO NOT MODIFY BELOW HERE
*{\\ rtf1\\ ansi\\ deff0{\\ fonttbl{\\ f0\\ fnil Tahoma;}}\n\\ viewkind4\\ uc1\\ pard\\ lang1030\\ f0\\ fs16 \n\\ par }
*/
